## Exception handling between OCaml and JS (@since 1.7.0)

In JS world, exception could be any data, while OCaml exception is structured data format and supports pattern match, catch OCaml exception on JS side is no-op.

### Catch JS exception

To catch Js exception on OCaml side, we categorize all JS exceptions to belong to `Js.Exn.Error`.

[source,ocaml]
--------------
let example1 () =
    match Js.Json.exnParse {| {"x" }|} with
    | exception Js.Exn.Error err ->
        Js.log @@ Js.Exn.stack err;
        None
    | v -> Some v

let example2 () =
    try Some (Js.Json.exnParse {| {"x"}|}) with
    Js.Exn.Error _ -> None
--------------

The exception definition of `Js.Exn.Error` is as below:

[source,ocaml]
--------------
type t =
  < stack : string Js.undefined ;
    message : string Js.undefined ;
    name : string Js.undefined;
    fileName : string Js.undefined
  > Js.t

exception Error of t
--------------

### Raise JS style exception

We provide such functions

[source,ocaml]
--------------
(** Raise Js exception Error object with stacktrace *)
val error : string -> 'a
val evalError : string -> 'a
val rangeError : string -> 'a
val referenceError : string -> 'a
val syntaxError : string -> 'a
val typeError : string -> 'a
val uriError : string -> 'a
--------------

Please consult module link:../api/Js.Exn.html[`Js.Exn`] for more details


## `bs.open`: Type safe external data-source handling (@@since 1.7.0)

There are some cases, the data-source could either come from JS land or OCaml land, it is very hard to give precise type information.
For example, for an external promise whose creation could come from JS API, its failed value caused by `Promise.reject` could be in any shape.

BuckleScript provides a solution to filter out OCaml structured exception data from the mixed data source,
it preserves the type safety while allow users to deal with mixed source.

It makes use of OCaml's extensible variant, so that users can mix values of type `exn` with JS data-source

.Example
[source,ocaml]
--------------
let handleData = function [@bs.open]
   | Invalid_argument _ -> 0
   | Not_found -> 1
   | Sys_error _ -> 2

val handleData : 'a -> int option // <1>
--------------
<1> For any input source, as long as it matches the exception pattern (nested pattern match supported), the matched value is returned, otherwise return `None`.


### Use cases

Take promise for example:

[source,ocaml]
--------------
let v = Js.Promise.reject Not_found
let handlePromiseFailure = function [@bs.open]
   | Not_found -> Js.log "Not found"; (Js.Promise.resolve ())

let () =
   v
   |> Js.Promise.catch (fun error ->
        match handlePromiseFailure error with
        | Some x -> x
        | None -> raise UnhandledPromise
    )
--------------
